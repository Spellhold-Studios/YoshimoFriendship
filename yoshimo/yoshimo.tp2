// --------------------------------------
// Yoshimo Friendship Setup
//
// Author(s): K'aeloree
// --------------------------------------
// Backup directory
BACKUP ~yoshimo/backup~

// Error reports sent to K'aeloree
//AUTHOR ~kae@spellholdstudios.net~
SUPPORT ~http://www.shsforums.net/forum/574-yoshimo-friendship/~

// Version
VERSION ~v4.6~

// Readme
README ~yoshimo/readme/yoshimofriendship-readme-%LANGUAGE%.html~ ~yoshimo/readme/yoshimofriendship-readme-english.html~

// Translations
AUTO_TRA ~yoshimo/translations/%s~

ALWAYS

	/* eet compatibility */
	ACTION_IF GAME_IS ~eet~ BEGIN
		OUTER_SET bg2_chapter = 12
	END ELSE BEGIN
		OUTER_SET bg2_chapter = 0
	END
	OUTER_FOR (i=1 ; i<=10 ; i=i+1) BEGIN
		OUTER_SET bg2_chapter = bg2_chapter + 1
		OUTER_SPRINT name_source ~bg2_chapter_%i%~
		OUTER_SET EVAL ~%name_source%~ = bg2_chapter
	END

	/* Converts tra files to UTF-8 */
	// No exception, everything needs to be converted
	ACTION_DEFINE_ARRAY YoFrienoconvert BEGIN END

	// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
	ACTION_DEFINE_ARRAY YoFrienreload BEGIN Setup END

	LAF HANDLE_CHARSETS
		INT_VAR
			infer_charsets = 1
		STR_VAR
			tra_path = EVAL ~%MOD_FOLDER%/translations~
			noconvert_array = YoFrienoconvert
			reload_array = YoFrienreload
	END

END


// Languages
LANGUAGE ~English~
         ~english~
         ~yoshimo/translations/english/setup.tra~
         
LANGUAGE ~Polski (translation by Lava Del'Vortel)~
         ~polski~
         ~yoshimo/translations/polski/setup.tra~

LANGUAGE ~Francais (traduction : Isaya, Ixyon et Graoumf)~
         ~french~
         ~yoshimo/translations/french/setup.tra~

LANGUAGE ~Chinese (translation by Merzbau)~
         ~schinese~
         ~yoshimo/translations/schinese/setup.tra~

LANGUAGE ~Russian (translation by Arkie)~
         ~russian~
         ~yoshimo/translations/russian/setup.tra~


/* ===================================== *
 *    Yoshimo Friendship for BG2: ToB    *
 * ===================================== */

BEGIN @0
REQUIRE_PREDICATE GAME_IS ~bg2ee bgt tob eet~ @1 /* The Yoshimo Friendship is only compatible with BGII: ToB, BGT and BGII:EE installations. */
DESIGNATED 0 LABEL ~yoshimo_friendship~

/* CamDawg's custom IsValidForPartyDialogue() state. Thanks Cam!
   covers STATE_CONFUSED, STATE_FEEBLEMINDED, STATE_SILENCED, STATE_DEAD, STATE_ACID_DEATH, STATE_FLAME_DEATH, STATE_EXPLODING_DEATH, STATE_STONE_DEATH, STATE_FROZEN_DEATH, STATE_HELPLESS, STATE_STUNNED, STATE_PANIC, STATE_BERSERK, STATE_SLEEPING */
APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~

// Compiling dialogue...
COMPILE ~yoshimo/dialogue/yoshj.d~
  USING ~yoshimo/translations/english/yoshj.tra~
        ~yoshimo/translations/%s/yoshj.tra~

// Compiling scripts...
EXTEND_BOTTOM ~yoshimo.bcs~ ~yoshimo/scripts/yoshimo.baf~ EVALUATE_BUFFER

// Adding new triggers, changing timing etc for existing Yoshimo talks
COPY_EXISTING ~byoshim.dlg~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~GlobalTimerExpired("YoshimoTalksPC","GLOBAL")~ ~RealGlobalTimerExpired("YoshimoTalksPC","GLOBAL")~                                               // Ensuring talk #1 triggers
		REPLACE_TEXTUALLY ~SetGlobal("BYoshimo3","LOCALS",1)~ ~RealSetGlobalTimer("I#YoshimoTalksPC2","GLOBAL",3600) SetGlobal("BYoshimo3","LOCALS",1)~                     // Ensuring talk #2 triggers
		REPLACE_TEXTUALLY ~GlobalTimerExpired("YoshimoTalksPC2","GLOBAL")~ ~RealGlobalTimerExpired("I#YoshimoTalksPC2","GLOBAL")~                                           // Ensuring talk #2 triggers
		REPLACE_TEXTUALLY ~SetGlobalTimer("YoshimoTalksPC2","GLOBAL",TEN_DAYS)~ ~RealSetGlobalTimer("YoshimoTalksPC3","GLOBAL",3600) SetGlobal("I#Yoshimo_BY5","GLOBAL",1)~ // Ensuring talk #3 triggers, if it's meant to
		REPLACE_TEXTUALLY ~Global("BYoshimo5","LOCALS",0)~ ~Global("BYoshimo5","LOCALS",0) Global("I#Yoshimo_BY5","GLOBAL",2)~                                              // Changing triggers for talk #3 to ensure it triggers
	END
BUT_ONLY

COPY_EXISTING ~yoshimo.dlg~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~SetGlobalTimer("YoshimoTalksPC","GLOBAL",FIVE_DAYS)~ ~RealSetGlobalTimer("YoshimoTalksPC","GLOBAL",3600)~
	END
BUT_ONLY
  
COPY_EXISTING ~yoshimo.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~GlobalTimerExpired("YoshimoTalksPC","GLOBAL")~ ~RealGlobalTimerExpired("YoshimoTalksPC","GLOBAL")~
		REPLACE_TEXTUALLY ~GlobalTimerExpired("YoshimoTalksPC2","GLOBAL")~ ~RealGlobalTimerExpired("I#YoshimoTalksPC2","GLOBAL")~
	END
BUT_ONLY
  
// Audio
COPY ~yoshimo/audio/i#blank.wav~ ~override~